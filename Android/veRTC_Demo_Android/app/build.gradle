plugins {
    id 'com.android.application'
}

android {

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    compileSdkVersion project.compileSdkVersion

    defaultConfig {
        applicationId "rtc.vertcdemo.android"
        minSdkVersion project.minSdkVersion
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion project.targetSdkVersion

        versionCode 1
        versionName project.APP_VERSION_NAME

        ndk { abiFilters 'arm64-v8a', 'armeabi-v7a' }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        manifestPlaceholders.put("APPLOG_SCHEME", "".toLowerCase())

        multiDexEnabled true

        buildConfigField('String', 'URL_PRIVACY_AGREEMENT', '"https://www.volcengine.com/docs/6348/68918"')
        buildConfigField('String', 'URL_USER_AGREEMENT', '"https://www.volcengine.com/docs/6348/128955"')
        buildConfigField('String', 'URL_DISCLAIMER', '"https://www.volcengine.com/docs/6348/68916"')
        buildConfigField('String', 'URL_SERVICE_AGREEMENT', '"https://www.volcengine.com/docs/6348/68917"')
        buildConfigField('String', 'URL_USED_PERMISSIONS', '"https://www.volcengine.com/docs/6348/71438"')
        buildConfigField('String', 'URL_SDK_LIST', '"https://www.volcengine.com/docs/6348/66812"')
        buildConfigField('String', 'SOLUTION_ORDER', "\"${project.SOLUTION_ORDER}\"")
    }

    signingConfigs {
        release {
            storeFile file("bytertc.jks")
            storePassword "bytertc"
            keyAlias "bytertc"
            keyPassword "bytertc"
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.release
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main {
            jniLibs {
                srcDir {
                    "libs/jniLibs"
                }
            }
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                String versionStr = project.APP_VERSION_NAME
                String suffix = versionStr.replace('.', '')
                outputFileName = "veRTC_Demo${suffix}.apk"
                println('产物地址: ' + outputFile.path)
            }
        }
    }
}

repositories {
    flatDir {
        dirs 'libs', project(':scene-core').files('libs')
    }
}

dependencies {
    implementation "androidx.appcompat:appcompat:$AppCompatVersion"
    implementation "androidx.recyclerview:recyclerview:$RecyclerViewVersion"
    implementation "androidx.constraintlayout:constraintlayout:$ConstraintLayoutVersion"
    implementation "com.google.android.material:material:$MaterialVersion"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    implementation "org.greenrobot:eventbus:$EventBusVersion"

    implementation "com.github.bumptech.glide:glide:$GlideVersion"
    annotationProcessor "com.github.bumptech.glide:compiler:$GlideVersion"

    implementation project(":component:$project.LOGIN_MODULE")
    implementation project(":scene-core")

    implementation project(":solutions:voicechat")
}